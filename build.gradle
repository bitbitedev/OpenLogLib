plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'signing'
}

group = 'dev.bitbite'
version = '0.2.0'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
    withJavadocJar()
    withSourcesJar()
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			groupId = 'dev.bitbite'
			artifactId = 'OpenLogLib'
			version = '0.2.0'
			pom {
				name = 'OpenLogLib - Logging made easy'
				description = 'OpenLogLib is a easy to use library for logging messages by providing the required methods, while keeping the API lightweight and logical.'
				url = 'https://github.com/bitbitedev/openloglib'
				licenses {
					license {
						name = 'MIT License'
						url = 'https://github.com/bitbitedev/openloglib/blob/main/LICENSE'
					}
				}
				developers {
					developer {
						id = 'bitbite'
						name = 'Bitbite Team'
						email = 'team@bitbite.dev'
						organization = 'bitbite'
						organizationUrl = 'https://bitbite.dev'
					}
				}
				scm {
					connection = 'scm:git:git://gihub.com/bitbitedev/openloglib.git'
					url = 'https://github.com/bitbitedev/openloglib'
				}
			}
		}
	}
	repositories {
		maven {
			name = "OSSRH"
			credentials {
				username = System.getenv("MAVEN_USERNAME")
				password = System.getenv("MAVEN_PASSWORD")
			}
			def releasesRepositoryURL = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			def snapshotRepositoryURL = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
			url = version.endsWith('SNAPSHOT') ? snapshotRepositoryURL : releasesRepositoryURL
		}
	}
}

signing {
	sign publishing.publications.maven
}

artifacts {
    archives javadocJar, sourcesJar
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}
dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'

    implementation 'dev.thatsnasu:Ansi:1.1.1'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}
